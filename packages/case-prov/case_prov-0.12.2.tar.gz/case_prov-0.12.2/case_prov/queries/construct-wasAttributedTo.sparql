# This software was developed at the National Institute of Standards
# and Technology by employees of the Federal Government in the course
# of their official duties. Pursuant to title 17 Section 105 of the
# United States Code this software is not subject to copyright
# protection and is in the public domain. NIST assumes no
# responsibility whatsoever for its use by other parties, and makes
# no guarantees, expressed or implied, about its quality,
# reliability, or any other characteristic.
#
# We would appreciate acknowledgement if the software is used.

CONSTRUCT
{
  # Designate InvestigativeAction results as associated with the action
  # performer or the tool operating on their behalf.
  ?nResult prov:wasAttributedTo ?nAgent .
}
WHERE {
  ?nInvestigativeAction
    a case-investigation:InvestigativeAction ;
    uco-action:result ?nResult ;
    .

  {
    SELECT ?nInvestigativeAction ?nAgent
    WHERE {
      ?nInvestigativeAction
        uco-action:instrument ?nAgent ;
        .
      FILTER NOT EXISTS {
        ?nInvestigativeAction
          uco-action:performer ?nPerformer ;
      }
    }
  }
  UNION
  {
    SELECT ?nInvestigativeAction ?nAgent
    WHERE {
      ?nInvestigativeAction
        uco-action:performer ?nAgent ;
        .
      FILTER NOT EXISTS {
        ?nInvestigativeAction
          uco-action:instrument ?nInstrument ;
      }
    }
  }
  UNION
  {
    SELECT ?nInvestigativeAction ?nAgent
    WHERE {
      ?nInvestigativeAction
        uco-action:performer ?nPerformer ;
        uco-action:instrument ?nAgent ;
        .
    }
  }
}
