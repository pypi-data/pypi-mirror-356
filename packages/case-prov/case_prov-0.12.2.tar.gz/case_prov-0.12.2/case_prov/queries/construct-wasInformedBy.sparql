# This software was developed at the National Institute of Standards
# and Technology by employees of the Federal Government in the course
# of their official duties. Pursuant to title 17 Section 105 of the
# United States Code this software is not subject to copyright
# protection and is in the public domain. NIST assumes no
# responsibility whatsoever for its use by other parties, and makes
# no guarantees, expressed or implied, about its quality,
# reliability, or any other characteristic.
#
# We would appreciate acknowledgement if the software is used.

CONSTRUCT
{
  # Identify all actions chained together by passing some intermediary
  # result, A -> result -> B.
  #
  # The latest provenance record for an object is used.  The latest
  # provenance record may or may not have been made by the action that
  # generated the result.
  ?nUsingAction prov:wasInformedBy ?nMostRecentAction .
}
WHERE {
  # ?nResult might be processed by multiple actions, but cannot be
  # generated by multiple actions.  The designation for having been
  # processed multiple times is to be a member of multiple provenance
  # records.
  #
  # The subquery selects all actions that are tied together by some
  # object, regardless of the object's type.  The rdf:type assertion on
  # ?nMostRecentProvenanceRecord confirms the actions are CASE
  # Investigative Actions, and that the tying objects include a
  # Provenance Record.
  #
  # For query performance reasons, the type assertions on
  # ?nMostRecentAction and ?nUsingAction are not reviewed.  This query
  # assumes that Actions using or resulting in CASE Provenance Records
  # are CASE Investigative Actions.
  ?nMostRecentProvenanceRecord a case-investigation:ProvenanceRecord .
  # ?nMostRecentAction a case-investigation:InvestigativeAction .
  # ?nUsingAction a case-investigation:InvestigativeAction .

  {
    ?nMostRecentAction uco-action:result ?nMostRecentProvenanceRecord .
    ?nUsingAction uco-action:object ?nMostRecentProvenanceRecord .
  }

  ?nUsingAction uco-action:object ?nResult .
  ?nMostRecentProvenanceRecord uco-core:object ?nResult .
}
